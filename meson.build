project('PyRATP', 'c',
  version : '1.0.0',
  license: 'CeCILL-C',
  meson_version: '>=0.64.0',
  default_options : ['warning_level=0'],
)

add_languages('fortran')

subdir('src/openalea/ratp')


py_mod = import('python')
py = py_mod.find_installation(pure: false)
py_dep = py.dependency()

sources = files([
  'src/f90/mod_Constant_ValuesF2PY.f90',
  'src/f90/mod_Grid3DF2PY_64bit.f90',
  'src/f90/mod_SkyvaultF2PY.f90',
  'src/f90/mod_Vegetation_TypesF2PY.f90',
  'src/f90/mod_Dir_InterceptionF2PY.f90',
  'src/f90/mod_Hemi_InterceptionF2PY.f90',
  'src/f90/mod_MicrometeoF2PY.f90',
  'src/f90/mod_Shortwave_BalanceF2PY.f90',
  'src/f90/mod_Energy_BalanceF2PY.f90',
  'src/f90/mod_PhotosynthesisF2PY.f90',
  'src/f90/mod_MinerPhenoF2PY.f90',
  'src/f90/mod_prog_RATP.f90'
])

incdir_numpy = run_command(py,
  ['-c', 'import os; os.chdir(".."); import numpy; print(numpy.get_include())'],
  check : true
).stdout().strip()

incdir_f2py = run_command(py,
    ['-c', 'import os; os.chdir(".."); import numpy.f2py; print(numpy.f2py.get_include())'],
    check : true
).stdout().strip()

ratp_source = custom_target('pyratpmodule.c',
  input : sources,
  output : ['pyratpmodule.c', 'pyratp-f2pywrappers2.f90'],
  command : [py, '-m', 'numpy.f2py', '@INPUT@', '-m', 'pyratp', '--lower']
)

inc_np = include_directories(incdir_numpy, incdir_f2py)

py.extension_module('pyratp',
  [sources, ratp_source],
  incdir_f2py / 'fortranobject.c',
  include_directories: inc_np,
  dependencies : py_dep,
  install : true
)
